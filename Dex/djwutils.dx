-- djwutils.dx
-- Some generic utility functions
-- some of which should probably be added to the standard Dex prelude

def iterate {a} (n: Nat) (step: a -> a) (init: a) : Fin n => a =
  with_state init \st.
    for i:(Fin n).
      old = get st
      next = step old
      st := next
      old

def unfold {a b} (n: Nat) (advance: a -> (b & a)) (init: a) : Fin n => b =
  with_state init \st.
    for i:(Fin n).
      (b, a) = advance (get st)
      st := a
      b

def length {a n} [Ix n] (arr: n=>a): Nat =
  size n

def last_dumb {a n} [Ix n] (arr: n=>a): a =
  head $ reverse arr

def last {a n} [Ix n] (arr: n=>a): a =
  nind = unsafe_nat_diff (size n) 1
  arr.(asidx nind)

-- Not ideal instance, since likely loss of precision...
instance Parse Float64
  parseString = \str.
    mf : Maybe Float = parseString str
    case mf of
      Nothing -> Nothing
      Just f -> Just $ f_to_f64 f

def clipv {a} (x: a=>Float) (c: Float): a=>Float =
  map (\xi. clip (-c, c) xi) x

def nanclip {a} (x: a=>Float): a=>Float =
  map (\xi. if (isnan xi) then 0.0 else xi) x

def to_tsv {n p} (mat: n=>p=>Float) : String =
  ms = for i j. show mat.i.j <> "\t"
  concat (map (\l. l <> "\n") (map concat ms))

-- based on "lines" from the prelude...
def words (sep: Word8) (source: String): List String =
  (AsList _ s) = source <> (to_list [sep])
  (AsList num_words space_ixs) = cat_maybes for i_char.
    if (s.i_char == sep)
      then Just i_char
      else Nothing
  to_list for i_word:(Fin num_words).
    start = case prev_ix i_word of
      Nothing -> first_ix
      Just i -> right_post space_ixs.i
    end = left_post space_ixs.i_word
    post_slice s start end

def cons {a} (x: a) (xs: List a): List a =
  (AsList on xt) = xs
  n = on + 1
  nxt = for i:(Fin n). case (ordinal i == 0) of
    True -> x
    False -> xt.(asidx (unsafe_nat_diff (ordinal i) 1))
  to_list nxt

def list2tab {n a} [Ix n] (l: List a): n=>a =
  (AsList ll t) = l
  unsafe_cast_table n t

def parse_tsv (sep: Word8) (input: String): List (List String) =
  (AsList _ lines) = lines input
  to_list $ map (words sep) lines


-- some Markov chain utilities

def step_n {s} (n: Nat) (kern: s -> Key -> s)
    (state: s) (k: Key) : s =
  yield_state state \st.
    for i:(Fin n).
      st := kern (get st) (ixkey k i)

def markov_chain {s} (init: s) (kern: s -> Key -> s) (its: Nat)
    (k: Key) : Fin its => s =
  with_state init \st.
    for i:(Fin its).
      x = kern (get st) (ixkey k i)
      st := x
      x


-- from Dex example file, examples/mcmc.dx
def meanAndCovariance {n d} (xs:n=>d=>Float) : (d=>Float & d=>d=>Float) =
   xsMean :    d=>Float = (for i. sum for j. xs.j.i) / n_to_f (size n)
   xsCov  : d=>d=>Float = (for i i'. sum for j.
                           (xs.j.i' - xsMean.i') *
                           (xs.j.i  - xsMean.i )   ) / (n_to_f (size n) - 1)
   (xsMean, xsCov)




-- eof
